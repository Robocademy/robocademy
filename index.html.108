17
/* Rain - Creates a rain effect for Arduino LoL Shield

 Created 8 January 2011
 by Todd Moore - http://toddmoore.com/ - @toddmoore
 
 Based on Life example code created by Jimmie Rodgers
 http://jimmieprodgers.com/kits/lolshield/
  
 This code is in the public domain.
*/

#include <avr/pgmspace.h>  // AVR library for writing to ROM
#include <Charliplexing.h> // Imports the library, which needs to be
                           // Initialized in setup.
                       
// customize these values
#define DELAY 100            // rain speed delay
#define RAIN 30              // percentage of rain (0-100)

// constants and globals
#define SIZEX 14              // lol shield width
#define SIZEY 9               // lol shield height
byte world[SIZEX][SIZEY];     // world of active leds

// sets x,y value of led and world
void set(int x, int y, int v)
{
     world[x][y] = v;
     LedSign::Set(x, y, v);
}

// returns random bit 0 or 1 baesd on rain percentage
int randBit()
{
  int v = random(100);
  return (v < RAIN) ? 1 : 0;
}

// set the world or a certain number of lines with random bits
void randWorld(int lines = SIZEY)
{
    // Builds the world with an initial seed.
    for (int i = 0; i < SIZEX; i++) 
    {
      for (int j = 0; j < lines; j++) 
      {
        set(i,j,randBit());
      }
    }
}

// create rain effect by dropping bits by one line
void rain()
{
    for (int i = 0; i < SIZEX; i++) 
    {
      for (int j = SIZEY-1; j > 0; --j) 
      {
        set(i,j, world[i][j-1]);
      }
    } 
}

// setup is called once for initializing
void setup() 
{
  // init the lol shield
  LedSign::Init();

  // randomize using analog seed value  
  randomSeed(analogRead(5));
  
  // fill the world with random bits
  randWorld();
}

void loop() 
{
  // create rain effect by moving bits down
  rain();
  
  // add a random line of bits to the top
  randWorld(1);
  
  // delay
  delay(DELAY);
}

